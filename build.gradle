buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

group= "mod.chiselsandbits"
archivesBaseName = "chiselsandbits"
if (System.getenv().BUILD_NUMBER != null) {//adds the build number to the end of the version string if on a build server
    project.version += ".${System.getenv().BUILD_NUMBER}"
}

repositories {
	maven { url "http://dvs1.progwml6.com/files/maven" }
	maven { url "http://maven.amadornes.com/" }
	flatDir { dirs 'libs' }
}

dependencies {
	deobfCompile "mezz.jei:${jei_version}"
	deobfCompile "MCMultiPart:${mcmp_version}"
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "eclipse"
    mappings = "snapshot_20160518"
    replaceIn "ChiselsAndBits.java"
    replace "@VERSION@", project.version
}

task apiJar(type: Jar) {
    from sourceSets.main.java
    include "mod/chiselsandbits/api/**"

    from sourceSets.main.output
    include "mod/chiselsandbits/api/**"

    classifier = 'api'
}
// prevent java 8's strict doclint for javadocs from failing builds
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }

jar {
	manifest {
		attributes 'FMLAT': 'chiselsandbits_at.cfg'
	}
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
   archives javadocJar
   archives sourcesJar
   archives apiJar
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    
    // Move access transformers to META-INF
    rename '(.+_at.cfg)', 'META-INF/$1'
}

task("uploadJars", dependsOn:"build") {
    description = "uploads JARs"
    if (project.hasProperty("local_maven")) {
        apply plugin: 'maven'
            uploadArchives {
                repositories {
                    mavenDeployer {
                        repository(url: "file://${local_maven}")
                }
            }
        }
    }
}
